#!/usr/bin/env perl
use strict;
use warnings;

use Cwd;

my $arg = shift @ARGV // "";
my $filename = getcwd() . "/" . $arg; # absolute path
my $line_start = shift @ARGV;
my $line_end   = shift @ARGV;

my $host = qx{git config --get hub.host} || "github.com";
chomp $host;

my ($remote_url) = qx{git config --get remote.origin.url};
chomp $remote_url;
my ($user, $repo) = $remote_url =~ qr{[:/]([^/]+)/([^/]+?)(?:[.]git)?$};

my $branch = qx{git symbolic-ref --short HEAD 2> /dev/null};
chomp($branch);

my $git_top = qx{git rev-parse --show-toplevel};
chomp $git_top;

$filename =~ s{^$git_top/}{};

my $url = "https://$host/$user/$repo/tree/$branch/$filename";
$url .= "#L$line_start" if $line_start;
$url .= "..L$line_end"  if $line_end;
system "open $url";
